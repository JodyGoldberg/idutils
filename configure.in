dnl Process this file with autoconf to produce a configure script.
AC_INIT(mkid.c)
AC_CONFIG_HEADER(config.h)

PACKAGE=mkid
VERSION=3.0
PATCH_LEVEL=6
OLD_PATCH_LEVEL=`expr $PATCH_LEVEL - 1`
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE_UNQUOTED(PATCH_LEVEL, "$PATCH_LEVEL")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(PATCH_LEVEL)
AC_SUBST(OLD_PATCH_LEVEL)

dnl Checks for programs.
AC_PROG_YACC
AC_SUBST(YACC)
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Checks for libraries.

dnl Checks for header files.
dnl AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(limits.h stddef.h sys/types.h sys/stat.h sys/param.h)
AC_CHECK_HEADERS(stdlib.h unistd.h string.h malloc.h memory.h assert.h ctype.h errno.h)
AC_CHECK_HEADERS(sys/ioctl.h termios.h termio.h sgttyb.h regex.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
fp_C_PROTOTYPES
AC_TYPE_SIGNAL
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_CHECK_SIZEOF(long)

CFLAGS=${CFLAGS--g}
LDFLAGS=${LDFLAGS--g}
AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(getwd getcwd getopt_long)
AC_CHECK_FUNCS(strdup strchr strrchr strcasecmp strerror)
AC_CHECK_FUNCS(memcpy bcopy memset bzero)
AC_CHECK_FUNCS(regcomp regcmp re_comp)
AC_FUNC_ALLOCA
AC_SUBST(ALLOCA)
if test "$ac_cv_func_re_comp" = no; then REGEXP="regex.o"; fi
AC_SUBST(REGEXP)
if test "$ac_cv_func_getopt_long" = no; then GETOPT="getopt.o getopt1.o"; fi
AC_SUBST(GETOPT)
if test "$ac_cv_func_strcasecmp" = no; then STRCASECMP="strcasecmp.o"; fi
AC_SUBST(STRCASECMP)
if test "$ac_cv_func_strerror" = no; then STRERROR="strerror.o"; fi
AC_SUBST(STRERROR)

if test "x$prefix" != xNONE; then
    libdir=$prefix/lib
else
    libdir=$ac_default_prefix/lib
fi
IID_HELP_FILE=$libdir/iid.help
AC_SUBST(libdir)
AC_SUBST(IID_HELP_FILE)
AC_DEFINE_UNQUOTED(IID_HELP_FILE, "$libdir/iid.help")

DEPEND=${srcdir:-.}/depend.out
DEPEND_FILE=$DEPEND
AC_SUBST(DEPEND)
AC_SUBST_FILE(DEPEND_FILE)

AC_OUTPUT(Makefile)
date >stamp-h
