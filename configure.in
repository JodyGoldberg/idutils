dnl Process this file with autoconf to produce a configure script.

##############################################################################
### Autoconf setup

AC_INIT(mkid.c)
AC_CONFIG_HEADER(config.h)

##############################################################################
### Package identification

PACKAGE=mkid
VERSION=3.0.9
PREV_VERSION=3.0.8
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(PREV_VERSION)

##############################################################################
### Checks for programs.

AC_PROG_YACC
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_INSTALL
AC_PROG_MAKE_SET

##############################################################################
### Checks for libraries.

##############################################################################
### Checks for header files.

dnl AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(limits.h stddef.h sys/types.h sys/stat.h sys/param.h)
AC_CHECK_HEADERS(stdlib.h unistd.h string.h malloc.h memory.h assert.h ctype.h errno.h)
AC_CHECK_HEADERS(sys/ioctl.h termios.h termio.h sgtty.h regex.h)

##############################################################################
### Checks for typedefs, structures, and compiler characteristics.

fp_C_PROTOTYPES
AC_C_CONST
AC_TYPE_SIGNAL
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_CHECK_TYPE(dev_t, unsigned short)
AC_CHECK_TYPE(ino_t, unsigned long)

CFLAGS=${CFLAGS--g}
LDFLAGS=${LDFLAGS--g}
AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)

##############################################################################
### Checks for library functions & system calls.

AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(getwd getcwd getopt_long basename dirname)
AC_CHECK_FUNCS(strdup strchr strrchr strcasecmp strerror)
AC_CHECK_FUNCS(memcpy bcopy memset bzero)
AC_CHECK_FUNCS(regcomp regcmp re_comp link)
AC_FUNC_ALLOCA
AC_SUBST(ALLOCA)
if test "$ac_cv_func_re_comp" = no; then REGEXP="regex.o"; fi
AC_SUBST(REGEXP)
if test "$ac_cv_func_getopt_long" = no; then GETOPT="getopt.o getopt1.o"; fi
AC_SUBST(GETOPT)
if test "$ac_cv_func_strcasecmp" = no; then STRCASECMP="strcasecmp.o"; fi
AC_SUBST(STRCASECMP)
if test "$ac_cv_func_strerror" = no; then STRERROR="strerror.o"; fi
AC_SUBST(STRERROR)

##############################################################################
### Directory locations.

if test "x$prefix" != xNONE; then
    datadir=$prefix/share
else
    datadir=$ac_default_prefix/share
fi
IID_HELP_FILE=$datadir/iid.help
AC_SUBST(datadir)
AC_SUBST(IID_HELP_FILE)
AC_DEFINE_UNQUOTED(IID_HELP_FILE, "$datadir/iid.help")

##############################################################################
### Makefile dependencies.

case x$srcdir in
x) DEPEND=depend.out;;
*) DEPEND=$srcdir/depend.out;;
esac
DEPEND_FILE=$DEPEND
AC_SUBST(DEPEND)
AC_SUBST_FILE(DEPEND_FILE)

##############################################################################
### Autoconf results generation.

AC_OUTPUT(Makefile,
    [test -z "$CONFIG_HEADERS" || echo timestamp >stamp-h])
